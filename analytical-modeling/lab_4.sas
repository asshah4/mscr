*--------------------------------------------------------------------------*
|TITLE: Lab 4                                                              |
|Introduction to Cox Proportional Hazards Models                           |
|                                                                          |
|                                                                          |
|DATE: February 11, 2020 in class exercise (WITHOUT ANSWERS)               |
|                                                                          |
|DATASETS available on Canvas                                              |                                             
|  1. Excel dataset named 'TBSurvival'                                     |
|                                                                          |
|                                                                          |
|USER: Matthew Magee                                                       |
*--------------------------------------------------------------------------*

*Lab 4 contains four parts: 

Part 1. Bringing in data using Import Wizard
Part 2. Multiple measures of association with prospective data
Part 3. Multivariable Cox models
Part 4. Simple interaction with Cox models;


********PART 1. BRINGING IN DATA USING IMPORT WIZARD;

*SAS datasets are common, but it is likely that you 
will be required to import data into SAS
that is in Excel for CSV format.

*Practice using SAS Import Wizard to import
the TBSurvival dataset.

1. Save the Excel file from Canvas onto the 
desktop or other computer location. 
2. From the file menu, select 'Import Data' 
and follow the guided options to import Excel data.
3. When selecting the table to import, make sure to select, 
"use data in first row as SAS variable names" 
4. Save the data as a temporary file named 'one';

* Read in data;
PROC IMPORT 
	OUT = WORK.tbsurv 
		DATAFILE= "C:\Users\asshah4\Box Sync\projects\mscr\analytical-modeling\TBSurvival.xlsx"
		DBMS=xlsx REPLACE;
	GETNAMES=YES;
RUN;

*Check that log to make sure the import worked.

*How many observations are there? 780
How many variables? 24
Does this match what you expect? Yes

*Perform proc contents to determine if the variable 
names were imported correctly;
PROC CONTENTS DATA = tbsurv;
RUN;

* It added a space on "_foreign", will fix that;
DATA tbsurv;
	RENAME _foreign = foreign;
	SET tbsurv;
RUN;

*Use a libname statement and a data step to make 
the temporary dataset 'one' a permanent SAS
dataset named TBsurv;

LIBNAME H "C:\Users\asshah4\Box Sync\projects\mscr\analytical-modeling\";
RUN;

DATA H.tbsurv;
	SET WORK.tbsurv;
RUN;

*Last, export the permanent SAS dataset as a CSV file and save it to the desktop.

1. Use the Export Wizard to select the data, choose the export file type, and saved location.
2. Save the code generated by SAS to export the data;

*Export code:;
PROC EXPORT DATA = H.tbsurv
	OUTFILE = "C:\Users\asshah4\Box Sync\projects\mscr\analytical-modeling\tbsurv.csv"
	DBMS = CSV REPLACE;
RUN;


***PART 2. MULTIPLE MEASURES OF ASSOCIATION WITH PROSPECTIVE DATA;

*For Part 2, download the Lab 4 Table Shells from Canvas;

*The goal for Part 2 is to complete Table A;

*Table A reports relative measures of association for the 
relationship between biologic sex (exposure) and death (outcome);

*First explore the frequency table between sex and death;
PROC FREQ DATA = tbsurv;
	TABLE sex*death;
RUN;

*How many men died? 37
*How many women died? 7
*What was the overall cumulative incidence for death? 5.64%

***Report the crude odds ratio and 95% CI 
with females as the referent group;

*This can be done and number of ways, but easily with 
proc logistic;
*Model 2.1;
PROC LOGISTIC DATA = tbsurv;
	CLASS sex (REF = '0');
	MODEL death (EVENT = '1') = sex;
RUN;

*Odds ratio is: 2.418 (1.062, 5.504)

***Report the crude risk ratio and 95% CI 
with females as the referent group;

*This can be done a couple ways, but easiest is 
probably proc freq;
PROC FREQ DATA = tbsurv ORDER = freq;
	TABLE sex*death / CHISQ RELRISK;
RUN;

*Double check by hand, what should answer should be? 
- 2.418


***Report the crude rate ratio and 95%CI with 
females as the referent group;

*There are also multiple ways to do this. 
For simplicity, sum the survtime for women/men using 
proc print and sum;

*Then calculate the rate ratio by hand. 
The 95% CI can be calculated by OpenEpi;

PROC PRINT DATA = tbsurv;
	SUM survtime death; *the 'sum' statement in proc print 
	will sum vertically across the variables indicated; 
	WHERE sex=0; *choose '0' for women;
RUN;

*total person days for women: 60609 (and 7 deaths);

*Now repeat for men;
PROC PRINT DATA = tbsurv;
	SUM survtime death;
	WHERE sex = 1;
RUN;
*total person days for men: 140964 (with 37 deaths);

*Use OpenEpi for 95% CI: 
https://www.openepi.com/PersonTime2/PersonTime2.htm 

Rate ratio and 95%CI: 2.273	(1.055, 5.514);


***Report the crude hazard ratio and 95%CI 
with females as the referent group;

*Model 2.2;
PROC PHREG DATA = tbsurv;
	CLASS sex (REF = '0');
	MODEL survtime*death(0) = sex / RL;
RUN;



*Crude hazard rate ratio and 95% CI: 2.32 (0.97, 5.57)

*Note: 7 observatoins were not used. Why? 
- Because survival time is missing

*Explore survival time and death status of 
those observations not used?;
PROC UNIVARIATE DATA = tbsurv;
	VAR survtime;
RUN;

*How did the odds ratio, risk ratio, and rate ratio 
handle the observations missing?
- they don't exclude them, place them in the denominator

*What would happen to the HR if you assumed all 
those with missing survival time died
before follow-up began?

Use a data step to create a new survival time variable that 
assigns a survival time of 1 day to those who died 
with missing surival information. 

Then rerun the HR estimate (model 2.2);

DATA temp;
	SET tbsurv;
	IF survtime = . AND death = 1 THEN survtime = 1;
RUN;

*Check that recoding worked;
PROC FREQ DATA = temp;
	TABLE survtime;
	WHERE death = 1;
RUN;

PROC PRINT DATA = temp;
	VAR sex death survtime;
	WHERE survtime = .;
RUN;



*rerun the HR estimate with the new surival time and assumption about death before treatment;
*Model 2.3;
PROC PHREG DATA = temp;
	CLASS sex(REF = '0');
	MODEL survtime*death(0) = sex / RL;
RUN;

*What are the results in the Model 2.3? 
*HR does not change much (2.31) but significance does: 
- 95%CI 1.03, 5.18;

********PART 3. MULTIVARIABLE COX MODELS;

*For Part 3, download the Lab 4 Table Shells from Canvas;

*The goal for Part 3 is to complete Table B from the Shell Table;

*Column 2 "Converted"
*Report the proportion that achieved conversion by age group;
PROC FREQ DATA = tbsurv;
	TABLE converted*age_group;
RUN;

*Column 3 "Median days to conversion (IQR)
*Use proc univariate to obtain median days to conversion;

*15-24 age group;
PROC UNIVARIATE DATA = tbsurv;
	VAR convtime;
	WHERE converted = 1 AND age_group = 1;
RUN;

*Caution, the median time should only be for those that converted;

*If converted = 0 then convtime is the date of censorship;

*25-44 age group;
PROC UNIVARIATE DATA = tbsurv;
	VAR convtime;
	WHERE converted = 1 AND age_group = 2;
RUN;

*45-64 age group;
PROC UNIVARIATE DATA = tbsurv;
	VAR convtime;
	WHERE converted = 1 AND age_group = 3;
RUN;

*>=65 age group;
PROC UNIVARIATE DATA = tbsurv;
	VAR convtime;
	WHERE converted = 1 AND age_group = 4;
RUN;

*Column 4 "cHR (95%CI)"
*Use proc phreg to obtain hazard rate ratio;

*choose 15-24 age group as referent group;
*Model 3.1;
PROC PHREG DATA = tbsurv;
	CLASS age_group (REF = '1');
	MODEL convtime*converted(0) = age_group / RL;
RUN;

*Interpret the HR ratio in Model 3.1 for >=65 
compared to 15-24 in one sentence:

Patients >= 65 years of age had a 1.17 increased HR for converting
to sputum negative compared to the youngest age group.

*Does this make sense?
*May be easier to conceptualize with the KM curve;

PROC LIFETEST DATA = tbsurv 
		METHOD = km 
		PLOTS = survival (TEST ATRISK);
	TIME convtime * converted(0);
	STRATA age_group;
RUN;

*Column 5 "aHR (95%CI)"
*Use proc phreg to obtain hazard rate ratio;
*Model 3.2;
PROC PHREG DATA = tbsurv;
	CLASS age_group (REF = '1');
	MODEL convtime*converted(0) = age_group hiv / RL;
RUN;


********PART 4. SIMPLE INTERACTION WITH COX MODELS;

*Assess statistical interaction between HIV (exposure) and sex (interacting term) with
hazard rate of sputum culture conversion;

*First estimate the HR ratio for the relationship between HIV and conversion;

*Model 4.1;
proc phreg data=matt.tbsurv;
class hiv (param=ref ref='0');
model convtime*converted(0)=hiv/rl;
run;
*What is the HR comparing HIV positive to negative: 1.53;

*To get a general sense of what the expected outcome will be,
rerun model 4.1 separately for men and women using a 'where' statement;

*Model 4.2 for women;
proc phreg data=matt.tbsurv;
????
run;
*Model 4.2 for men;
proc phreg data=matt.tbsurv;
????
run;
*what is the effect of HIV on hazard rate of conversion among women: ?
*what is the effect of HIV on hazard rate of conversion among men: ?

*Next, assess the interaction between HIV and sex using a product term in the model;
 
*Traditional way is to create a product term in a data step;
data three;
set matt.tbsurv;
hiv_sex=hiv*sex;
run;

*Model 4.3;
proc phreg data=three;
model convtime*converted(0)=hiv sex hiv_sex;
contrast 'HR for HIV among sex=0' hiv 1/estimate=exp; *Contrast statement is needed to indicate to SAS
which covariates should be contrasted;
contrast 'HR for HIV among sex=1' hiv 1 hiv_sex 1/estimate=exp;
run;

*This way works too;
*Model 4.4;
proc phreg data=three;
model convtime*converted(0)=hiv sex hiv*sex;
contrast 'HR for HIV among sex=0' hiv 1/estimate=exp;
contrast 'HR for HIV among sex=1' hiv 1 hiv*sex 1/estimate=exp;
run;

